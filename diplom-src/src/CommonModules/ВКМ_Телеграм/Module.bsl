
//{{ВКМ. Реализуем функционал отправки сообщений Телеграм-боту
Функция ВКМ_ОтправитьВТелеграм(ТекстСообщения) Экспорт
	
	Попытка
		//ВКМ. Из Констант получаем: Access token и ChatId
		AccessToken = Константы.ВКМ_ТокенУправленияТелеграммБотом.Получить(); 
		ChatId = Константы.ВКМ_ИдентификаторГруппыДляОповещения.Получить();
		
		//ВКМ. Отправляем сообщение с указанными AccessToken, ChatId и ТекстСообщения
		ОтправкаВТелеграм = Новый HTTPСоединение("api.telegram.org",443,,,,15,Новый ЗащищенноеСоединениеOpenSSL());
		Результат = ОтправкаВТелеграм.Получить(Новый HTTPЗапрос("bot" + AccessToken + "/sendMessage?chat_id=" + ChatId + "&text=" + ТекстСообщения));
	Исключение
		// ВКМ. Если запрос не дошел до HTTP-Сервера
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Произошла сетевая ошибка!";
		Сообщение.Сообщить();
		ВызватьИсключение;
	КонецПопытки;
	
	// ВКМ. Формируем сообщение об ошибке и записываем его в Журнал регистрации
	Если Результат.КодСостояния <> 200 Тогда  
		
		Тело = Результат.ПолучитьТелоКакСтроку();  
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(Тело);
		ДанныеОшибки = ПрочитатьJSON(Чтение, Истина); 
		Чтение.Закрыть();
		
		ИнформацияОбОшибке = ДанныеОшибки.Получить("description");
		КодОшибки = ДанныеОшибки.Получить("error_code");
		ЗаписьОбОшибке = СтрШаблон("Ошибка: %1, код ошибки: %2", ИнформацияОбОшибке, КодОшибки);
		
		ЗаписьЖурналаРегистрации("HTTPСервисы.Ошибка", УровеньЖурналаРегистрации.Ошибка,,,ЗаписьОбОшибке);
		
	КонецЕсли;
	
КонецФункции 
//}}ВКМ 

//{{ВКМ. Реализуем функционал Регламентного задания отправки сообщений Телеграм-боту 
Процедура ВКМ_ОтправкаУведомленийВТелеграмм() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВКМ_УведомленияТелеграмБоту.ВКМ_ТекстСообщения КАК ВКМ_ТекстСообщения
		|ИЗ
		|	Справочник.ВКМ_УведомленияТелеграмБоту КАК ВКМ_УведомленияТелеграмБоту";
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		ТекстСообщения = ВыборкаДетальныеЗаписи.ВКМ_ТекстСообщения;
		ВКМ_ОтправитьВТелеграм(ТекстСообщения);
	КонецЦикла; 
	
	// После отправки уведомлений Телеграм-боту удаляем элементы справочника ВКМ_УведомленияТелеграмБоту
	Выборка = Справочники.ВКМ_УведомленияТелеграмБоту.Выбрать();
	Пока Выборка.Следующий() Цикл 
		 ОбъектСправочника = Выборка.ПолучитьОбъект();
		 ОбъектСправочника.Удалить();
	КонецЦикла;
	
КонецПроцедуры
//}}ВКМ 
